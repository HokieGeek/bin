#!/bin/sh

threshold=1
terminal="xterm"
mfu_file="$HOME/.dmenu_run_mfu"
[ ! -f ${mfu_file} ] && touch ${mfu_file}

while [ $# -gt 0 ]; do
    case $1 in
    --threshold) shift; threshold=$1 ;;
    --terminal) shift; terminal=$1 ;;
    *) dmenu_args="${dmenu_args}$1 " ;;
    esac
    shift
done

dmenu_path | awk -v threshold=${threshold} '
FILENAME == "-" && !($0 in mfu) {
    print
    next
}
$1 > threshold {
    $1 = ""
    $2 = ""
    sub("^ *", "")
    sub(" *$", "")
    mfu[$0]=0
    if (length($0) > 0) {
        print
    }
}
' ${mfu_file} - \
| dmenu $dmenu_args \
| awk -v shell=${SHELL:-"/bin/sh"} -v terminal=${terminal} '
BEGIN {
    ageOut = strftime("%s")-((86400 * 7) * 8)
}
FILENAME == "-" {
    if (length($0) > 0 && $0 !~ "^ *$") {
        sel = $0
        if (substr(sel, length(sel), 1) == "#") {
            sub("#$", "", sel)
            system(sprintf("%s -e \"%s\"", terminal, sel))
        } else {
            print sel | shell
            close(shell)
        }
    } else {
        exit 1
    }
    next
}
$2 <= ageOut {
    next
}
$0 !~ /^ *$/ {
    cmd=""
    for (i=NF; i >= 3; i--) {
        cmd=$i" "cmd
    }
    sub(" *$", "", cmd)
    mfu[cmd] = $1
    mfu_timestamp[cmd] = $2
}
END {
    if (!(sel in mfu)) {
        mfu[sel] = 1
    } else {
        mfu[sel]++
    }
    mfu_timestamp[sel] = strftime("%s")

    for (cmd in mfu) {
        mfu_out = sprintf("%s%s %s %s\n", mfu_out, mfu[cmd], mfu_timestamp[cmd], cmd)
    }
    print mfu_out | sprintf("sort -gr > %s", FILENAME)
}
' - ${mfu_file} &
