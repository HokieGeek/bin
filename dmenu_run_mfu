#!/bin/sh

threshold=1
terminal="xterm"
list="$HOME/.dmenu_run_mfu"
# dmenu_args=""

while [ $# -gt 0 ]; do
    case $1 in
    --threshold) shift; threshold=$1 ;;
    --terminal) shift; terminal=$1 ;;
    *) dmenu_args="${dmenu_args}$1 " ;;
    esac
    shift
done

dmenu_path | awk -v threshold=${threshold} -v mfu_file=${list} '
BEGIN {
    while((getline < mfu_file) > 0) {
        if ($1 > threshold) {
            cnt = $1
            $1 = ""
            $2 = ""
            sub("^ *", "")
            sub(" *$", "")
            mfu[$0] = cnt
            print
        }
    }
    close(mfu_file)
}
!($0 in mfu) {
    print
}
' \
| dmenu $dmenu_args \
| awk -v shell=${SHELL:-"/bin/sh"} -v terminal=${terminal} '
BEGIN {
    ageOut = strftime("%s")-((86400 * 7) * 8)
}
FILENAME == "-" {
    sel = $0
    next
}
$2 <= ageOut {
    next
}
$0 !~ /^ *$/ {
    cnt = $1
    ts = $2
    $1 = ""
    $2 = ""
    sub("^ *", "")
    sub(" *$", "")
    mfu[$0] = cnt
    if (sel == $0) {
        mfu[$0]++
        mfu_timestamp[$0] = strftime("%s")
    } else {
        mfu_timestamp[$0] = ts
    }
}
END {
    if (length(sel) > 0 && sel !~ "^ *$") {
        if (!(sel in mfu)) {
            mfu[sel] = 1
            mfu_timestamp[sel] = strftime("%s")
        }
        for (cmd in mfu) {
            mfu_out = sprintf( "%s%s %s %s\n", mfu_out, mfu[cmd], mfu_timestamp[cmd], cmd)
        }
        new_mfu = sprintf("sort -gr > %s", FILENAME)
        print mfu_out | new_mfu
        close(new_mfu)

        if (substr(sel, length(sel), 1) == "#") {
            sub("#$", "", sel)
            system(sprintf("%s -e \"%s\"", terminal, sel))
        } else {
            print sel | shell
            close(shell)
        }
    }
}
' - ${list} &
